<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <meta http-equiv="X-UA-Compatible" content="ie=edge">
  <title>37. Random Pattern 1</title>
  <style>
    html,
    body {
      margin: 0;
      padding: 0;
    }
    canvas {
      display: block;
    }
    #opt {
      position: fixed;
      top: 10px;
      right: 10px;
    }
  </style>
  <script src="https://cdn.bootcss.com/gl-matrix/2.8.1/gl-matrix-min.js"></script>
  <script src="https://cdn.bootcss.com/dat-gui/0.7.6/dat.gui.min.js"></script>
  <script src="./initProgram.js"></script>
</head>
<body>
  <canvas id="test" tabindex="1"></canvas>
  <script type="shader-source" id="vertex">
    precision mediump float; // 中等精度
    attribute vec3 a_Pos; // 点坐标
    varying vec3 v_Pos; // 插值点坐标
    void main() {
      gl_Position = vec4(a_Pos * vec3(1.0, -1.0, 1.0), 1.0); // 点坐标（屏幕坐标的y坐标轴是由下往上的）
      v_Pos = a_Pos;
    }
  </script>
  <script type="shader-source" id="fragment">
    #define PI 3.14159265359
    precision mediump float; // 中等精度
    uniform float u_Time; // 当前时间戳
    uniform vec2 u_Mouse; // 当前鼠标坐标
    uniform vec2 u_Screen; // 窗口大小
    uniform float u_Seed; // 随机数种子
    uniform float u_Mode; // 图案模式
    varying vec3 v_Pos; // 插值点坐标
    float aspect = u_Screen.x / u_Screen.y; // 宽高比
    float TWO_PI = 2.0 * PI;

    // 2维伪随机数
    float random2d (vec2 st) {
      float n = dot(st, vec2(
        -19.841,
        86.63
      ));

      return fract(sin(n * 64720.764) + u_Seed);
    }

    // 10 PRINT CHR$(205.5+RND(1)); : GOTO 10 迷宫生成器
    float maze (vec2 ipos, vec2 fpos) {
      float seed = random2d(ipos);
      float k = seed < 0.5 ? -1.0 : 1.0;
      vec2 p = fpos - 0.5;
      float target = k * p.x;
      return smoothstep(target - 0.1, target - 0.05, p.y) - smoothstep(target + 0.05, target + 0.1, p.y);
    }

    // 三角形
    float tri (vec2 ipos, vec2 fpos) {
      float seed = random2d(ipos);
      float k = seed < 0.5 ? -1.0 : 1.0;
      vec2 p = fpos - 0.5;
      float target = k * p.x;
      return smoothstep(target, target + 0.01, p.y);
    }

    // 圆弧拼接
    float circle (vec2 ipos, vec2 fpos) {
      float seed = random2d(ipos);
      vec2 p = fpos - 0.5;
      float len1 = 0.0;
      float len2 = 0.0;

      if (seed < 0.5) { // 1, 3
        len1 = length(0.5 - p);
        len2 = length(-0.5 - p);
      } else { // 2, 4
        len1 = length(vec2(-0.5, 0.5) - p);
        len2 = length(vec2(0.5, -0.5) - p);
      }

      return smoothstep(0.44, 0.45, len1) - smoothstep(0.54, 0.55, len1)
           + smoothstep(0.44, 0.45, len2) - smoothstep(0.54, 0.55, len2);
    }

    // 图案
    float pattern (vec2 ipos, vec2 fpos) {
      float res = 0.0;
      if (u_Mode == 0.0) {
        res = maze(ipos, fpos);
      } else if (u_Mode == 1.0) {
        res = tri(ipos, fpos);
      } else if (u_Mode == 2.0) {
        res = circle(ipos, fpos);
      }

      return res;
    }

    void main() {
      vec2 pos = v_Pos.xy * vec2(aspect, 1.0);
      vec2 mPos = u_Mouse * vec2(aspect, 1.0);
      vec2 num = vec2(6.0, 6.0);
      vec2 ipos = floor(pos * num); // 整数索引
      vec2 fpos = fract(pos * num);
      vec3 color = vec3(pattern(ipos, fpos));
      gl_FragColor = vec4(color, 1.0); // 片元颜色
    }
  </script>
  <script>
    let cur = Date.now()
    let guiInfo = {
      mode: 0
    }
    let gui = new dat.GUI()
    let demo = new Program({
      needScreen: true,
      needMouse: true,
      updateTime: true,
      loop: true,
      uniform: {
        u_Type: {
          type: '1f',
          value: 0
        },
        u_Seed: {
          type: '1f',
          value: cur / 1000 - Math.floor(cur / 1000)
        },
        u_Mode: {
          type: '1f',
          value: guiInfo.mode
        }
      }
    })
    gui
      .add(guiInfo, 'mode', {
        maze: 0,
        triangle: 1,
        circle: 2})
      .name('图案模式')
      .onFinishChange(() => {
        demo.updateUniform('u_Mode', guiInfo.mode)
      })
    demo.start()
  </script>
</body>
</html>